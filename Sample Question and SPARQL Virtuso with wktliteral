
# 1.Object - Location/Footprint:
Q1.1) Where is the "Schlehdorf" gauging station located within the catchment area? [What are the coordinates] - ## Tested and working

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX schema: <https://schema.org/>

SELECT ?geomObj ?easting ?northing ?elevation
WHERE { GRAPH <http://hydroturtle/LamahCE>
{
    # Find the sensor with the name "Bangs"
    ?sensor schema:name "Bangs" ;
            geo:hasGeometry ?geom .
    
    # Get the WKT representation as geomatric object
    ?geom geo:asWKT ?geomObj.
    
    # Extract coordinates using Virtuoso spatial functions
    BIND (bif:st_x(?geomObj) AS ?easting)   # Longitude
    BIND (bif:st_y(?geomObj) AS ?northing)  # Latitude
    BIND (bif:st_z(?geomObj) AS ?elevation) # Elevation
}}


Q1.2) Where are the gauging stations located in the catchment areas for maximum three average annual precipitations in 2015? 

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
PREFIX qudt: <https://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX unit: <https://qudt.org/vocab/unit/>
PREFIX schema: <https://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?stationName ?easting ?northing (SUM(?flowRate) AS ?totalFlowRate) ?unit
FROM <http://hydroturtle/LamahCE>
WHERE { GRAPH <http://hydroturtle/LamahCE>
{
     # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) 
     ?observation rdf:type sosa:Observation ;
               sosa:observedProperty envthes:30106 ;
               sosa:madeBySensor ?station ;
               sosa:resultTime ?resultTime ;
               sosa:hasResult ?result .

      #Filter results of 2015
      FILTER(YEAR(?resultTime) = 2015)

      # Access the blank node for QuantityValue
     ?result rdf:type qudt:QuantityValue ;
           qudt:numericValue ?flowRate;
           qudt:unit ?unit.

     # Retrieve gauging station and location details
     ?station rdf:type sosa:Sensor ;
           schema:name ?stationName ;
          geo:hasGeometry ?locationGeometry .

    ## Get the WKT representation
    ?locationGeometry geo:asWKT ?geomObj.

   
    # Extract coordinates using Virtuoso spatial functions
    BIND (bif:st_x(?geomObj) AS ?easting)   # Longitude
    BIND (bif:st_y(?geomObj) AS ?northing)  # Latitude

}
}

# Group by station to sum precipitation values (this one is for flow rate for now) for each station in 2015
GROUP BY ?stationName ?easting ?northing ?unit
ORDER BY DESC(?totalFlowRate)
LIMIT 3

Q1.3) Where are five of the largest catchment areas in this dataset?

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <https://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?stationName ?catchment ?catchmentArea 
WHERE { GRAPH <http://hydroturtle/LamahCE>
{
       #find the station name 
       ?sensor schema:name ?stationName;
                    n4e_hyd:monitorsCatchment ?catchment.

       # Find the catchment area
       ?catchment n4e_hyd:catchment_area_calculated ?catchmentArea.
}
}
# take the largest catchment area.  
ORDER BY DESC(?catchmentArea)
LIMIT 5

# 2. Object - Boundary: 
Q2.1) What are the neighbouring catchments of the catchment with the highest precipitation in the decade 2001-2010?  

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
PREFIX qudt: <https://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX unit: <https://qudt.org/vocab/unit/>
PREFIX schema: <https://schema.org/>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>

SELECT ?neighbourCatchment ?catchment ?totalPrecipitation ?geomObjCatNeig ?unit
#(COUNT(DISTINCT ?neighbourCatchment ) AS ?count) (COUNT(DISTINCT ?catchment ) AS ?count2) 
WHERE {
       GRAPH <http://hydroturtle/LamahCE>
       {
              # Find the catchment with the highest flow rate between 2001-2010
              {
                     SELECT ?catchment ?geomObjCat (SUM(?precipitation) AS ?totalPrecipitation) ?unit
                     WHERE {
                            # Find the observations with precipitation[envthes:30106]
                            ?observation rdf:type sosa:Observation ;
                            sosa:observedProperty envthes:30106;
                            sosa:madeBySensor ?sensor ;
                            sosa:resultTime ?time ;
                            sosa:hasResult ?result.

                            ## take the flow rate value
                            ?result qudt:numericValue ?precipitation;
                                   qudt:unit  ?unit.

                            # Take the catchment geometry as WKT
                            ?sensor n4e_hyd:monitorsCatchment ?catchment .
                            ?catchment geo:hasGeometry ?catchmentGeometry.
                            ?catchmentGeometry geo:asWKT ?geomObjCat.

                            # take the year from the time variable
                            BIND(YEAR(?time) as ?year)

                            # Filter the result duration for the decade between 2001 to 2010
                            FILTER (?year >=2001 && ?year <= 2010)
                            }
                            GROUP BY ?catchment ?geomObjCat ?unit
                            ORDER BY DESC(?totalPrecipitation)
                            LIMIT 1
                     }
                            
                     #from here this should try to take 
                     ?neighbourCatchment rdf:type envthes:30212 ;
                            geo:hasGeometry ?neighbourCatchmentGeom .

                     ?neighbourCatchmentGeom geo:asWKT ?geomObjCatNeig.


                     FILTER (?neighbourCatchment != ?catchment) # Exclude the max flow catchment
                     FILTER bif:st_intersects(?geomObjCat, ?geomObjCatNeig, 0) # 0 distance means touching borders
                     }
              }
              GROUP BY ?neighbourCatchment 

Q2.2) What is the average annual precipitation in catchment_1 for the period between 2010 and 2020?

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
PREFIX qudt: <https://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX unit: <https://qudt.org/vocab/unit/>
PREFIX schema: <https://schema.org/>

SELECT ?guagingStationName (AVG(?yearlyPrecipitation) AS ?averageAnnualPrecipitation) ?unit
WHERE { GRAPH <http://hydroturtle/LamahCE>
       {
              {
                     SELECT ?resultYear (SUM(?precipitation) AS ?yearlyPrecipitation) ?guagingStationName ?unit
                     WHERE {
                     
                     # Find the observations with precipitation[envthes:30106] 
                     # precipitation can be written as directly associalted with catchment as observerd property 
                     ?observation rdf:type sosa:Observation ;
                            sosa:observedProperty envthes:30106 ;
                            sosa:madeBySensor ?sensor ;
                            sosa:resultTime ?resultTime ;
                            sosa:hasResult ?result.

                     ## # Access the blank node to retrieve the precipitation value 
                     ?result qudt:numericValue ?precipitation;
                            qudt:unit  ?unit.

                     # find catchment related with sensor 
                     ?sensor n4e_hyd:monitorsCatchment n4e_hyd:catchment_1;
                            schema:name ?guagingStationName

                     # Extract the year directly in the FILTER and assign to ?year [this query is written between 2012 to 2015 for the moment data is uploaded]
                     BIND (YEAR(?resultTime) AS ?resultYear)
                     FILTER (?resultYear >= 2012 && ?resultYear <= 2015)
              }
              GROUP BY ?resultYear ?guagingStationName ?unit
       }
}
}


# Object - Spatial Properties:
3.5.1) What are the all-time minimum and maximum river flow observed at all the gauging stations?

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
PREFIX qudt: <https://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX unit: <https://qudt.org/vocab/unit/>
PREFIX schema: <https://schema.org/>
PREFIX locn: <http://www.w3.org/ns/locn#>

SELECT ?guagingStationName (MIN(?flowRate) AS ?minRiverFlow) (MAX(?flowRate) AS ?maxRiverFlow) ?unit
FROM <http://hydroturtle/LamahCE>
WHERE { GRAPH <http://hydroturtle/LamahCE>
       {
              # Find the observations with precipitation[envthes:30106] (flow rate for this time - envthes:21242) - 
              # Precipitation can be written as directly associated with catchment as observed property 
              ?observation rdf:type sosa:Observation ;
                     sosa:observedProperty envthes:21242 ;
                     sosa:madeBySensor ?sensor ;
                     sosa:resultTime ?resultTime ;
                     sosa:hasResult ?result.

              # Access the blank node to retrieve the precipitation value [for this case, used flow rate due to uploaded data for the moment to triple store ]
              ?result qudt:numericValue ?flowRate;
                     qudt:unit  ?unit.

              # find gauging station name  
              ?sensor n4e_hyd:monitorsCatchment ?catchment;
                     schema:name ?guagingStationName #"Schwarzach"    ## use directly station name instead of ?guagingStationName variable only particular station data need to be extracted. 
              }
       }
       GROUP BY ?guagingStationName ?unit #can remove GROUP BY - specially, only one gauging station is used. 


Q3.6) How many annual frost days (days with temperatures below 0°C) were recorded at the "Schwarzach" gauging station from 2010 to 2020? 

PREFIX n4e_hyd: <https://nfdi4earth.pages.rwth-aachen.de/knowledgehub/nfdi4earth-ontology/test_hyd#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX envthes: <http://vocabs.lter-europe.net/EnvThes/>
PREFIX qudt: <https://qudt.org/schema/qudt/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <https://schema.org/>

SELECT ?year (COUNT(?resultDate) AS ?noFrostDays)
WHERE {GRAPH <http://hydroturtle/LamahCE>
       {
              # Because the freezing is related to min air temp -  temperature observations at the "Schwarzach" station for AirTemperature2mAVG (for this min temp also can be used)
              ?observation rdf:type sosa:Observation ;
                     sosa:observedProperty n4e_hyd:AirTemperature2mAvg ;
                     sosa:madeBySensor ?sensor ;
                     sosa:resultTime ?resultDate ;
                     sosa:hasResult ?result .
              
              # Filter for the "Schwarzach" station and dates between 2010 and 2020 #only "Bangs" catchment TS is uploaded for now. therefore it used. 
              ?sensor rdf:type sosa:Sensor ;
                     schema:name "Schwarzach" .

              FILTER (YEAR(?resultDate) >= 2010 && YEAR(?resultDate) <= 2020)
       
              # Get temperature value and check if it's below 0°C
              ?result qudt:numericValue ?temperature .
              FILTER (?temperature < 0)
       
              # Extract the year for grouping
              BIND(YEAR(?resultDate) AS ?year)
       }
}
GROUP BY ?year
ORDER BY ?year




